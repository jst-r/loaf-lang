WHITESPACE = _{ " " | "\t" | NEWLINE }

integer = @{
    ASCII_DIGIT+
}

identifier = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }

primary = _{
    integer
  | identifier
  | "(" ~ expr ~ ")"
}

bin_op = _{ add | subtract | multiply | divide }

add      = { "+" }
subtract = { "-" }
multiply = { "*" }
divide   = { "/" }

prefix = _{ negate }
negate =  { "-" }

expr = { prefix? ~ primary ~ (bin_op ~ prefix? ~ primary)* }

expr_stmt =  { expr ~ ";" }
statement = _{ expr_stmt }

var_declaration =  { "let" ~ identifier ~ "=" ~ expr ~ ";" }
declaration     = _{ var_declaration | statement }

program = _{ SOI ~ declaration* ~ EOI }
